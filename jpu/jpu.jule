// jpu API
// Copyright 2025 Adam Perkowski.
// Use of this source code is governed by the BSD 3-Clause License.
// See the LICENSE file for details.

#pass "-lcurl -lcrypto"

use sbx "snapbox"
use sbh "snapbox/header"
use sbs "snapbox/status"
use "std/conv"
use "std/os"
use "std/os/filepath"
use ss "std/strings"

cpp use "crypto.h"

const tempDir = "/tmp/jpu"

static headerMap: sbh::HeaderMap = {
	sbh::ACCEPT: "application/vnd.github+json",
	sbh::USER_AGENT: "jpu",
	"X-GitHub-Api-Version": "2022-11-28",
}

cpp fn sha512sum(str): str

struct Proton {
	tagName:      str
	downloadURLs: []str
	protonDir:    str
}

impl Proton {
	fn Download(self)! {
		os::Dir.Create(tempDir) else {}

		tempProtonFile := tempDir + "/" + self.tagName + ".tar.gz"
		tempChecksumFile := tempDir + "/" + self.tagName + ".sha512sum"

		tempProtonURL := self.downloadURLs[2]
		tempChecksumURL := self.downloadURLs[1]

		sbx::Download(tempChecksumURL, tempChecksumFile) else {
			error("Failed to download `" + tempChecksumURL + "`")
		}
		sbx::Download(tempProtonURL, tempProtonFile) else {
			error("Failed to download `" + tempProtonURL + "`")
		}

		println("  Validating checksums...")
		tempProtonContent := os::File.Read(tempProtonFile) else {
			error("Failed to read `" + tempProtonFile + "`")
		}
		tempChecksumContent := os::File.Read(tempChecksumFile) else {
			error("Failed to read `" + tempChecksumFile + "`")
		}

		checksum := cpp.sha512sum(str(tempProtonContent))

		if !ss::Contains(str(tempChecksumContent), checksum) {
			cleanup([tempProtonFile, tempChecksumFile])
			error("Checksum mismatch")
		}
	}

	fn Install(self)! {
		tempProtonFile := tempDir + "/" + self.tagName + ".tar.gz"
		tempChecksumFile := tempDir + "/" + self.tagName + ".sha512sum"

		extractTarGz(tempProtonFile, filepath::Dir(self.protonDir)) else {
			cleanup([tempProtonFile, tempChecksumFile])
			error("Failed to extract `" + tempProtonFile + "`")
		}

		cleanup([tempProtonFile, tempChecksumFile])
	}
}

fn GetProton(tagRaw: str, urlRaw: str, mut installDir: str)!: Proton {
	mut tag := ""

	if tagRaw == "" {
		tag = "/latest"
	} else {
		tag = "/tags/" + tagRaw
	}

	url := urlRaw + tag
	request := sbx::GET(url).Headers(headerMap).Send()

	if !sbs::IsSuccess(request.status) {
		error("Failed to fetch `" + url + "`: " + conv::Itoa(int(request.status)))
	}

	data := request.body

	tags := getJSONValueN(data, "tag_name", 1) else {
		error(error)
	}
	mut downloadURLs := getJSONValueN(data, "browser_download_url", 2) else {
		error(error)
	}
	downloadSizes := getJSONValueN(data, "size", 2) else {
		error(error)
	}
	releaseDate := getJSONValueN(data, "published_at", 1) else {
		error(error)
	}
	tagName := tags[1]
	downloadSize := conv::Atoi(downloadSizes[2])!
	publishedAt := ss::Replace(ss::Replace(releaseDate[1], "T", " ", 1), "Z", "", 1)

	if ss::HasPrefix(installDir, "~") {
		home := os::Getenv("HOME")
		if home == "" {
			error("`$HOME` environment variable not set")
		}
		installDir = ss::Replace(installDir, "~", home, 1)
	}

	if !ss::HasSuffix(installDir, "/") {
		installDir += "/"
	}

	protonDir := installDir + tagName

	println("  Ready to download " + tagName)
	print("    Size: ")
	print(downloadSize / 1048576)
	println(" MB\n    Date: " + publishedAt)
	print("  Continue? [Y/n]: ")

	input := os::Stdin().ReadLine()!
	match ss::ToLower(input) {
	| "n":
		error("Aborted")
	}

	os::Dir.Read(protonDir) else {
		ret Proton{tagName, downloadURLs, protonDir}
	}

	error(tagName + " is already installed")
}

fn getJSONValueN(data: str, key: str, n: int)!: []str {
	if ss::Contains(data, key+`":`) == false {
		error("Key `" + key + "` not found")
	}

	mut values := ss::SplitN(data, key+`":`, n+1)

	for i, v in values {
		if i == 0 {
			values[i] = ""
			continue
		}

		if values[i][0] == '"' || values[i][0] == ' ' && values[i][1] == '"' {
			values[i] = ss::SplitN(v, `"`, 3)[1]
		} else {
			values[i] = ss::SplitN(v, `,`, 3)[0]
		}
	}

	ret values
}

fn extractTarGz(file: str, dir: str)! {
	os::Dir.Create(dir) else {
		error(error)
	}

	mut cmd := os::Cmd.New("tar")
	cmd.Args = ["-xzf", file, "-C", dir]

	cmd.Spawn() else {
		error(error)
	}
	cmd.Wait() else {
		error(error)
	}
}

fn cleanup(additional: []str) {
	println("  Cleaning up...")

	for _, f in additional {
		os::File.Remove(f) else {}
	}

	os::Dir.Remove(tempDir) else {}
}